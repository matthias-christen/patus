stencil wave (
	float grid U(0 .. x_max-1, 0 .. y_max-1, 0 .. z_max-1), 
	float param fMin = -1,
	float param fDX = 2 / (x_max-3),
	float param fDT_DX_sq = 0.25)
{
	iterate while t < 5;
	domainsize = (2 .. x_max-3, 2 .. y_max-3, 2 .. z_max-3);
  
	initial
	{
		U[x, y, z; -1] = sinf (2*π * ((x-1) * fDX + fMin)) * sinf (2*π * ((y-1) * fDX + fMin)) * sinf (2*π * ((z-1) * fDX + fMin));
		U[x, y, z; -1 : x==0 || y==0 || z==0 || x==x_max-1 || y==y_max-1 || z==z_max-1 ] = 0;
		U[x, y, z; 0] = U[x, y, z; -1];
		U[x, y, z; 1] = 0;
	}

	operation
	{
		float c1 = 2 - 15/2 * fDT_DX_sq;
		float c2 = 4/3 * fDT_DX_sq;
		float c3 = -1/12 * fDT_DX_sq;
				
		U[x, y, z; t+1] = c1 * U[x, y, z; t] - U[x, y, z; t-1] +
			c2 * (U[x+1, y, z; t] + U[x-1, y, z; t] + U[x, y+1, z; t] + U[x, y-1, z; t] + U[x, y, z+1; t] + U[x, y, z-1; t]) +
			c3 * (U[x+2, y, z; t] + U[x-2, y, z; t] + U[x, y+2, z; t] + U[x, y-2, z; t] + U[x, y, z+2; t] + U[x, y, z-2; t]);	            
	}
}
