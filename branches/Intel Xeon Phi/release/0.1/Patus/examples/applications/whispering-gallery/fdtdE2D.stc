/**
 * Stencil specification for the E-field update.
 */
stencil fdtdE2D (
	float grid e(1 .. x_max, 1 .. y_max)[2],
   	const float grid h(1 .. x_max, 1 .. y_max),
	const float grid ca(1 .. x_max, 1 .. y_max),
	const float grid cb(1 .. x_max, 1 .. y_max),
	float param ca_vacuum, float param ca_material,
	float param cb_vacuum, float param cb_material)
{
    domainsize = (2 .. x_max-1, 2 .. y_max-1);
    
    operation
    {
        e[x, y; t+1; 0] = ca[x, y] * e[x, y; t; 0] +
        	cb[x, y] * (h[x, y+1] - h[x, y]);
 
        e[x, y; t+1; 1] = ca[x, y] * e[x, y; t; 1] -
        	cb[x, y] * (h[x+1, y] - h[x, y]);
    }
    
    initial
    {
    	e[x, y; 0; 0] = 0;
    	e[x, y; 0; 1] = 0;
    	
		ca[x, y] = ca_vacuum;
		cb[x, y] = cb_vacuum;
		ca[x, y : (x-x_max/2)^2 + (y-y_max/2)^2 >= (2*x_max/5)^2] =
			ca_material;
		cb[x, y : (x-x_max/2)^2 + (y-y_max/2)^2 >= (2*x_max/5)^2] =
			cb_material;
    }
}
