stencil tricubic (
    float grid U(0 .. x_max+2, 0 .. y_max+2, 0 .. z_max+2),
    const float grid a(0 .. x_max+2, 0 .. y_max+2, 0 .. z_max+2),
    const float grid b(0 .. x_max+2, 0 .. y_max+2, 0 .. z_max+2),
    const float grid c(0 .. x_max+2, 0 .. y_max+2, 0 .. z_max+2))
{
	domainsize = (1 .. x_max, 1 .. y_max, 1 .. z_max);

	operation
	{
		float wa[-1..2] = {
			1.0/6.0                   * a[x,y,z] * (a[x,y,z]+1.0) * (a[x,y,z]+2.0),
			-0.5     * (a[x,y,z]-1.0)            * (a[x,y,z]+1.0) * (a[x,y,z]+2.0),
			 0.5     * (a[x,y,z]-1.0) * a[x,y,z]                  * (a[x,y,z]+2.0),
			-1.0/6.0 * (a[x,y,z]-1.0) * a[x,y,z] * (a[x,y,z]+1.0)
		};

		float wb[-1..2] = {
			1.0/6.0                   * b[x,y,z] * (b[x,y,z]+1.0) * (b[x,y,z]+2.0),
			-0.5     * (b[x,y,z]-1.0)            * (b[x,y,z]+1.0) * (b[x,y,z]+2.0),
			 0.5     * (b[x,y,z]-1.0) * b[x,y,z]                  * (b[x,y,z]+2.0),
			-1.0/6.0 * (b[x,y,z]-1.0) * b[x,y,z] * (b[x,y,z]+1.0)
		};

		float wc[-1..2] = {
			1.0/6.0                   * c[x,y,z] * (c[x,y,z]+1.0) * (c[x,y,z]+2.0),
			-0.5     * (c[x,y,z]-1.0)            * (c[x,y,z]+1.0) * (c[x,y,z]+2.0),
			 0.5     * (c[x,y,z]-1.0) * c[x,y,z]                  * (c[x,y,z]+2.0),
			-1.0/6.0 * (c[x,y,z]-1.0) * c[x,y,z] * (c[x,y,z]+1.0)
		};
		
		U[x, y, z; t+1] = { i=-1..2, j=-1..2, k=-1..2 } sum (wa[i] * wb[j] * wc[k] * U[x+i, y+j, z+k; t]);
	}
}
